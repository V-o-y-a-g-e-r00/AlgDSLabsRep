#! /usr/bin/gnuplot -persist
#Константы
LOOPOFFSET='5'
LOOPWEIGHTOFFSET='10'
EdgesLoopRadius=5;
set style line 1 lc rgb '#0000FF' lw 1   #Для ребер
set style line 2 lc rgb "green" lw 1
set style line 3 lc rgb "blue" lw 1
set style line 4 lc rgb "red" lw 1

set style line 11 lc rgb "white" #Для узлов
set style line 12 lc rgb "green"
set style line 13 lc rgb "blue"
set style line 14 lc rgb "red"

WeightOffset='0' #на петли не виляет


#Имена файлов с индексами и координатами точек; с информацией о ребрах; с легендой
flePnts = 'pnts.dat'
fleEdges = 'edges.dat'
GraphInfo= 'GraphInfo.dat'

#Отображаемая область xrange
x1=50
y1=50
set xr [-x1:x1]   
set yr [-y1:y1]

#Возможность читать стили из файла. (Чтобы определять, какими цветами будут окрашены узлы извне. Сами цвета устанавливаются тут)
set style increment user

#Устанавливаем одинаковый масштаб по осям
set size square   



#вот тут сложно. sprintf здесь это команда gnuplot. '' здесь кодируют одниночную кавычку.  '< gawk '' FNR==NR{x[$1]=$2;y[$1]=$3;next;}   {printf "%%f\t%%f\n%%f\t%%f\n\n", x[$1], y[$1], x[$2], y[$2];} '' %s %s'
#-здесь это форматная строка sprintf, которая как и положено заключена в одинарные кавычки. %s %s это, собственно, спецификаторы формата в этой строке (больше спецификаторов формата эта строка не имеет)
#flePnt fleEdges - это аргументы, подставляемые вместо спецификаторов формата.
#остальные команды относится сугубо к утилите gawk. (при построении графика вся вот эта длинная строчка вызывается в теримнале). 
#FNR==NR сравнение числа обработанных строк из текущего файла с числом строк, обработанных за все время работы. Вместе с командой next позволяет выполнять действие x[$1]=$2;y[$1]=$3; только для первого файла в списке файлов
#Т.е. x[$1]=$2;y[$1]=$3; заполняет массивы x и y значениями 2 и 3 столбцов файла pnts.dat Запись x[$1] означает, что индексы берутся из 1 столбца файла.После чего выполняется действие {printf "%%f\t%%f\n%%f\t%%f\n\n", x[$1], y[$1], x[$2], y[$2];} уже для второго файла
#Во втором файле у нас в 1 и 2 строках номера вершин, связянных друг с другом. т.е. в выходной строке у нас координаты начала и конца линий. Причем отдельные линии раделяются между собой пустой строкой
#printf в данном случае также команда gawk, а не bush или gnuplot
loadEdges = sprintf('< gawk '' FNR==NR{x[$1]=$2;y[$1]=$3;next;}   {printf "%%f\t%%f\t%%d\n%%f\t%%f\t%%d\n\n", x[$1], y[$1], $4, x[$2], y[$2], $4;} '' %s %s', flePnts, fleEdges);
loadEdgesLoops = sprintf('< gawk '' FNR==NR{x[$1]=$2;y[$1]=$3;next;}   {if($1==$2) printf "%%f\t%%f\t%%d\n", x[$1], y[$1]-%s, $4;} '' %s %s', LOOPOFFSET, flePnts, fleEdges);  

# gawk ' FNR==NR{x[$1]=$2;y[$1]=$3;next;}   {printf "%%f\t%%f\n%%f\t%%f\n\n", x[$1], y[$1], x[$2], y[$2];} ' pnts.dat edges.dat

loadWeightsNoLoops = sprintf('< gawk '' FNR==NR{x[$1]=$2;y[$1]=$3;next;} {if($1!=$2 && $2!="") printf "%%f\t%%f\t%%s\n", (x[$1]+x[$2])/2, (y[$1]+y[$2])/2 - %s, $3} '' %s %s',WeightOffset, flePnts, fleEdges);
  loadWeightsLoops = sprintf('< gawk '' FNR==NR{x[$1]=$2;y[$1]=$3;next;} {if($1==$2) printf "%%f\t%%f\t%%s\n", (x[$1]+x[$2])/2, (y[$1]+y[$2])/2 - %s, $3} '' %s %s',LOOPWEIGHTOFFSET, flePnts, fleEdges);
loadGraphInfo = sprintf('< gawk '' {n=$1;m=$2;} {printf "n=%%d\tm=%%d", n, m} '' %s', GraphInfo);


#Тут номера столбцов не везде совпадают с номерами столбцов в исходном файле. Т.к. gawk формирует строку, которая воспринимается как файл. И у неё свои столбцы
plot \
    loadEdgesLoops using 1:2:(EdgesLoopRadius):3 with circles lc var notitle, \
    loadEdges using 1:2:3 with lines lc var notitle, \
    flePnts using 2:3:(2):4 with circles fill solid lc var notitle, \
    flePnts using 2:3:(2) with circles lc rgb "black" notitle, \
    flePnts using 2:3:1 with labels tc rgb "black" font "Arial Bold" notitle, \
    loadWeightsNoLoops using 1:2 with points pt 5 lc rgb "blue" notitle, \
    loadWeightsNoLoops using 1:2:3 with labels tc rgb "black" center font "Arial Bold" notitle, \
    loadWeightsLoops using 1:2 with points pt 5 lc rgb "red" notitle, \
    loadWeightsLoops using 1:2:3 with labels tc rgb "black" center font "Arial Bold" notitle, \
    GraphInfo using (x1-11):(y1-5):1 with labels notitle

#plot \
#    loadEdges using 1:2 with lines lc rgb "black" lw 2 notitle, \ #рисуем
#    flePnts using 2:3:(0.6) with circles fill solid lc rgb "black" notitle, \ #рисуем кружки(узлы) (0.6) размер кружков
#    flePnts using 2:3:1 with labels tc rgb "white" font "Arial Bold" notitle, \ #рисуем текст в кружочках(узлах)
#    loadWeights using 1:2:3 with labels tc rgb "red" center font "Arial Bold" notitle


#Справка
# \ позволяет записать одну строку как несколько
# -persist позволяет держать окно открытым после завершения скрипта
# gawk -утилита для работы с файлами
#
#